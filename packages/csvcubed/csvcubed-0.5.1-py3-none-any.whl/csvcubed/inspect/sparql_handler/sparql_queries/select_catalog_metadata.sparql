PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT ?graph ?dataset ?distribution ?title ?label ?issued ?modified ?comment ?description
    ?license ?creator ?publisher ?landingPages ?themes ?keywords ?contactPoint ?buildActivity
    ?identifier
WHERE {
    {
        SELECT DISTINCT ?dataset
        WHERE {
            GRAPH ?someGraph {
                ?dataset a dcat:Dataset.
            }
        }
    }

    GRAPH ?graph {
        ?dataset dcterms:title ?title;
            rdfs:label ?label;
            dcterms:issued ?issued;
            dcterms:modified ?modified.
        OPTIONAL { ?dataset dcat:distribution ?distribution }.
        OPTIONAL { ?dataset rdfs:comment ?comment }.
        OPTIONAL { ?dataset dcterms:description ?description }.
        OPTIONAL { ?dataset dcterms:license ?license }.
        OPTIONAL { ?dataset dcterms:creator ?creator }.
        OPTIONAL { ?dataset dcterms:publisher ?publisher }.
        OPTIONAL { ?dataset dcat:contactPoint ?contactPoint }.
        OPTIONAL { ?dataset dcterms:identifier ?identifier }.
        OPTIONAL { ?dataset prov:wasGeneratedBy ?buildActivity }.
    }

    {
        # Select the (potentially multiple) themes as a `|` delimited string.
        SELECT ?graph ?dataset
            (GROUP_CONCAT(?theme; separator='|') as ?themes)
        WHERE {
                GRAPH ?graph {
                    ?dataset a dcat:Dataset.
                    OPTIONAL { ?dataset dcat:theme ?theme. }
                }
        }
        GROUP BY ?graph ?dataset
    }
    {
        # Select the (potentially multiple) keywords as a `|` delimited string.
        SELECT ?graph ?dataset
            (GROUP_CONCAT(?keyword; separator='|') as ?keywords)
        WHERE {
                GRAPH ?graph {
                    ?dataset a dcat:Dataset.
                    OPTIONAL { ?dataset dcat:keyword ?keyword. }
                }
        }
        GROUP BY ?graph ?dataset
    }
    {
        # Select the (potentially multiple) landing pages as a `|` delimited string.
        SELECT  ?graph ?dataset
            (GROUP_CONCAT(?landingPage ; separator='|') as ?landingPages)
        WHERE {
                GRAPH ?graph {
                    ?dataset a dcat:Dataset.
                    OPTIONAL { ?dataset dcat:landingPage ?landingPage. }
                }
        }
        GROUP BY ?graph ?dataset
    }
}
