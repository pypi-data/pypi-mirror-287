// The bison parser together with CEvaluationFlexLexer builds
// the evaluation tree.

%{

#define YYSTYPE CChemEqParserBase::Data *

#ifdef yylval
# undef yylval
#endif
#define yylval mpData

#ifdef yyerror
# undef yyerror
#endif
#define yyerror(__str) \
  if (yychar != YYERRCODE) correctErrorPosition(); \
  CCopasiMessage(CCopasiMessage::ERROR, MCFunction + 1, mPosition)

#include "copasi/copasi.h"
#include "copasi/model/CChemEqParser.h"

#include "copasi/utilities/CCopasiMessage.h"
#include "copasi/utilities/utility.h"

#undef yyparse
#define yyparse CChemEqParserBase::yyparse
%}

/* yacc Declarations */
%token TOKEN_NUMBER
%token TOKEN_MULTIPLY
%token TOKEN_PLUS
%token TOKEN_NAME
%token TOKEN_BEGIN_PRODUCTS
%token TOKEN_BEGIN_MODIFIERS
%token TOKEN_BEGIN_COMPARTMENT
%token TOKEN_END_COMPARTMENT

// Precedence

// Right Precedence

/* Grammar follows */
%%

reaction:  substrates products modifiers
           {
           }
         | substrates TOKEN_BEGIN_PRODUCTS modifiers
           {
             mReversibility = ($2->name == "=") ? true: false;
             pdelete($2);
           }
         | substrates products
           {
           }
         | substrates TOKEN_BEGIN_PRODUCTS
           {
             mReversibility = ($2->name == "=") ? true: false;
             pdelete($2);
           }
         | products modifiers
           {
           }

         | products
           {
           }

substrates: reactant 
           {
             mSubstrateNames.push_back($1->name);
             mSubstrateMult.push_back($1->multiplicity);
             mSubstrateCompartments.push_back($1->compartment);
             pdelete($1);
           }
         | substrates TOKEN_PLUS reactant
           {
             mSubstrateNames.push_back($3->name);
             mSubstrateMult.push_back($3->multiplicity);
             mSubstrateCompartments.push_back($3->compartment);
             pdelete($2);
             pdelete($3);
           }

products: TOKEN_BEGIN_PRODUCTS reactant 
           {
             mReversibility = ($1->name == "=") ? true: false;

             mProductNames.push_back($2->name);
             mProductMult.push_back($2->multiplicity);
             mProductCompartments.push_back($2->compartment);
             pdelete($1);
             pdelete($2);
           }
         | products TOKEN_PLUS reactant
           {
             mProductNames.push_back($3->name);
             mProductMult.push_back($3->multiplicity);
             mProductCompartments.push_back($3->compartment);
             pdelete($2);
             pdelete($3);
           }

modifiers: TOKEN_BEGIN_MODIFIERS reactant 
           {
             mModifierNames.push_back($2->name);
             mModifierMult.push_back($2->multiplicity);
             mModifierCompartments.push_back($2->compartment);
             pdelete($1);
             pdelete($2);
           }
         | modifiers reactant
           {
             mModifierNames.push_back($2->name);
             mModifierMult.push_back($2->multiplicity);
             mModifierCompartments.push_back($2->compartment);
             pdelete($2);
           }

reactant: prereactant
          {
            $$ = $1;
            $$->compartment = "";
          }  
        | prereactant compartment
          {
            $$ = $1;
            $$->compartment = $2->name;
            pdelete($2);
          }
        

prereactant: TOKEN_NAME
          {
            $$ = $1;
            $$->multiplicity = 1.0;
          }  
        | TOKEN_NUMBER TOKEN_NAME
          {
            $$ = $2;
            $$->multiplicity = strToDouble($1->name.c_str(), NULL);
            pdelete($1);
          }
        | TOKEN_NUMBER TOKEN_MULTIPLY TOKEN_NAME
          {
            $$ = $3;
            $$->multiplicity = strToDouble($1->name.c_str(), NULL);
            pdelete($1);
            pdelete($2);
          }
        
compartment: TOKEN_BEGIN_COMPARTMENT TOKEN_NAME TOKEN_END_COMPARTMENT
             {
               $$ = $2;
               pdelete($1);
               pdelete($3);
             }
;

%%

